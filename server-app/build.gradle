import java.text.SimpleDateFormat

plugins {
    id 'application'
}

mainClassName = 'eu.koboo.minestom.Launch'

dependencies {

    implementation "net.minestom:minestom-snapshots:${minestomVersion}"
    implementation "org.jetbrains:annotations:${annotationsVersion}"
    implementation "org.spongepowered:configurate-yaml:${configurateVersion}"

    implementation "org.slf4j:jul-to-slf4j:${slf4jVersion}"
    implementation "ch.qos.logback:logback-core:${logbackVersion}"
    implementation "ch.qos.logback:logback-classic:${logbackVersion}"

    implementation "org.fusesource.jansi:jansi:2.4.1"
    implementation "org.jline:jline:3.28.0"

    implementation project(':server-api')
}

def gitCommit = "git rev-parse --verify --short HEAD".execute().text.trim()
def gitBranch = "git rev-parse --abbrev-ref HEAD".execute().text.trim()

def buildGradle = "Gradle ${gradle.gradleVersion}"
def buildJDK = "JDK ${System.properties['java.version']} (${System.properties['java.vendor']})"
def buildJVM = "JVM ${System.properties['java.vm.version']} (${System.properties['java.vm.vendor']})"
def buildOS = "${System.properties['os.name']} (${System.properties['os.arch']} ${System.properties['os.version']})"

shadowJar {
    manifest {
        attributes(
                "Main-Class": mainClassName,
                "Implementation-Title": rootProject.name,
                "Implementation-Version": rootProject.version,
                "Implementation-Group": rootProject.group,
                "Git-Revision": gitCommit,
                "Git-Branch": gitBranch,
                "Build-Gradle": buildGradle,
                "Build-JDK": buildJDK,
                "Build-JVM": buildJVM,
                "Build-OS": buildOS
        )
    }
}

blossom {
    replaceToken("PROJECT_NAME", rootProject.name)
    replaceToken("PROJECT_VERSION", rootProject.version)
    replaceToken("PROJECT_GROUP", rootProject.group)
    replaceToken("DEP_MINESTOM", minestomVersion)
    replaceToken("GIT_COMMIT", gitCommit)
    replaceToken("GIT_BRANCH", gitBranch)
    replaceToken("BUILD_GRADLE", buildGradle)
    replaceToken("BUILD_JDK", buildJDK)
    replaceToken("BUILD_JVM", buildJVM)
    replaceToken("BUILD_OS", buildOS)
}
